import java.util.Properties

plugins {
    id("com.android.application")
    id("kotlin-android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

android {
    namespace = "com.qvasoft.qvasave_pro"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        isCoreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.qvasoft.qvasave_pro"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = 24 // extractor 插件要求 API 24+
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        create("release") {
            val keyPropertiesFile = file("../key.properties")
            if (keyPropertiesFile.exists()) {
                val keyProperties = Properties()
                keyProperties.load(keyPropertiesFile.inputStream())
                storeFile = file(keyProperties["storeFile"] as String)
                storePassword = keyProperties["storePassword"] as String
                keyAlias = keyProperties["keyAlias"] as String
                keyPassword = keyProperties["keyPassword"] as String
            }
        }
    }

    buildTypes {
        release {
            // 使用 release 签名配置
            signingConfig = signingConfigs.getByName("release")
            // R8 在 2026.07.11.2 开启后导致部分机型「打开即闪退」
            // （youtubedl-android / extractor / 通知插件依赖反射与 native，
            //  keep 规则尚未覆盖全量路径）。紧急回退到关闭混淆；
            // 后续补齐完整 keep 规则后再评估重新开启。
            isMinifyEnabled = false
            isShrinkResources = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url = uri("https://jitpack.io") }
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.2")
    implementation("androidx.core:core-ktx:1.13.1")
    // youtubedl-android 由 extractor 插件自动引入，无需手动添加
}

flutter {
    source = "../.."
}

// 禁用 Flutter Gradle Plugin 自动加的 ABI versionCode 偏移
// 让所有架构的 APK 使用相同版本号
android.applicationVariants.all {
    outputs.all {
        // 必须强转才能访问 versionCodeOverride（旧版 AGP API）
        (this as com.android.build.gradle.internal.api.ApkVariantOutputImpl).versionCodeOverride = flutter.versionCode.toInt()
    }
}
